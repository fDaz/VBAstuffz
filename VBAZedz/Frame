VERSION 5.00
Begin {C62A69F0-16DC-11CE-9E98-00AA00574A4F} Frame 
   Caption         =   "Pewpew"
   ClientHeight    =   12585
   ClientLeft      =   45
   ClientTop       =   375
   ClientWidth     =   12210
   OleObjectBlob   =   "Frame.frx":0000
   StartUpPosition =   1  'CenterOwner
End
Attribute VB_Name = "Frame"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Private pstats As New Scripting.Dictionary
Private bColl As New Scripting.Dictionary
Private zColl As New Scripting.Dictionary
Private zStat As New Scripting.Dictionary
Private Pews As New Scripting.Dictionary
Private player, healthBar As Image
Private fps, Pts, health, speed, shots, zeds, currAmm, relcount As Integer
Private firx, firy, spawn, spawnmax, fcyc As Double
Private upk, downk, rightk, leftk As Integer
Private firing, reload, switched As Boolean
Private running As Boolean

Private Sub UserForm_Terminate()

End

End Sub

Private Sub Panel_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)

Select Case KeyCode
    Case 87 'w
        upk = 1
    Case 65 'a
        leftk = 1
    Case 83 's
        downk = 1
    Case 68 'd
        rightk = 1
    Case 49 '1
        If switched = False Then
            pstats("pew") = "pistol"
            currAmm = 0
            relcount = 0
            switched = True
        End If
    Case 50 '2
        If switched = False Then
            pstats("pew") = "shotgun"
            currAmm = 0
            relcount = 0
            switched = True
        End If
    Case 51 '3
        If switched = False Then
            pstats("pew") = "smg"
            currAmm = 0
            relcount = 0
            switched = True
        End If
    Case 52 '4
        If switched = False Then
            pstats("pew") = "rifle"
            currAmm = 0
            relcount = 0
            switched = True
        End If
End Select

End Sub

Private Sub Panel_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)

Select Case KeyCode
    Case 87 'w
        upk = 0
    Case 65 'a
        leftk = 0
    Case 83 's
        downk = 0
    Case 68 'd
        rightk = 0
    Case 49 To 52
        switched = 0
End Select

End Sub

Private Sub Panel_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)

firx = X
firy = Y

firing = True

End Sub

Private Sub Panel_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)

If Button = 1 Then
    firx = X
    firy = Y
End If

End Sub

Private Sub Panel_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)

firing = False

End Sub

Sub Init()

running = True
fps = 0.01
speed = 2
Pts = 0
currAmm = 0
relcount = 0
reload = False
switched = False

shots = 0
zeds = 0

spawn = 0
spawnmax = 10

pstats.Add "hp", 100
pstats.Add "pew", "pistol"
fcyc = 999

Call InitPews

Set healthBar = Frame.Controls.Add("Forms.Image.1", "HPBar")
With healthBar
.Width = pstats("hp")
.Height = 12
.Top = 6
.Left = Frame.Width - 110
.BackColor = RGB(250, 0, 0)
End With

Set player = Frame.Panel.Controls.Add("Forms.Image.1", "Pchar")
With player
.Width = 10
.Height = 10
.Top = Frame.Height / 2
.Left = Frame.Width / 2
.BackColor = RGB(170, 100, 50)
End With

Call Main

End Sub

Sub Main()

Do

If currAmm = 0 Then reload = True

If reload = True And currAmm < Pews(pstats("pew"))("mag") Then
    relcount = relcount + 1
    If relcount = 100 Then
        reload = False
        relcount = 0
        currAmm = Pews(pstats("pew"))("mag")
    End If
End If

Call MovePlayer

If fcyc < Pews(pstats("pew"))("rof") Then
    fcyc = fcyc + 0.1
End If

If currAmm = 0 Then firing = False

If firing = True Then
    If fcyc >= Pews(pstats("pew"))("rof") Then
        fcyc = 0
        currAmm = currAmm - 1
        For i = 1 To Pews(pstats("pew"))("proj")
            Call ShootGun
        Next
    End If
    If Pews(pstats("pew"))("auto") = False Then firing = False
End If

If zColl.Count > 0 Then
    For Each zkey In zColl.Keys
        Call MoveZed(zkey)
    Next
End If

If bColl.Count > 0 Then
    For Each dkey In bColl.Keys
        Call MoveBullet(dkey)
    Next
End If

If bColl.Count > 0 Then
    For Each dkey In bColl.Keys
        Call BulletColl(dkey)
    Next
End If

healthBar.Width = pstats("hp")
Frame.Pewlbl.Caption = pstats("pew")
Frame.Ammlbl.Caption = currAmm
Frame.Rellbl.Caption = relcount

spawn = spawn + 0.1

If spawnmax <= spawn Then
    spawn = 0
    Call Spawner
End If

If pstats("hp") <= 0 Then running = False

Call Wait

Loop While running = True

MsgBox "Game Over!"

End Sub

Sub Spawner()

Randomize
whatto = zeds - Int((zeds + 1) * Rnd)

Select Case whatto
    Case 0 To 15
        Call NewZed(2, 5, 1, 10, 20, 250, 20)
    Case 16 To 40
        Call NewZed(4, 5, 2, 10, 20, 200, 20)
    Case 41 To 100
        Call NewZed(6, 5, 2, 20, 20, 150, 20)
    Case Else
        Call NewZed(6, 5, 2, 20, 20, 150, 20)
End Select

'spawnmax = spawnmax / 1.01

End Sub

Sub NewZed(zSp, bmax, hp, dam, r, g, b)

Dim zVars As New Scripting.Dictionary

zeds = zeds + 1

zVars.Add "bcount", 0
zVars.Add "bmax", bmax
zVars.Add "hp", hp
zVars.Add "dam", dam

Randomize
wfrom = Int(4 * Rnd)

Select Case wfrom
    Case 1 'arrive from up
        Randomize
        xPos = Int(Panel.Width + 21 * Rnd - 10)
        yPos = -10
    Case 2 'arrive from down
        Randomize
        xPos = Int(Panel.Width + 21 * Rnd - 10)
        yPos = Panel.Height + 10
    Case 3 'arrive from left
        xPos = -10
        Randomize
        yPos = Int(Panel.Height + 21 * Rnd - 10)
    Case 4 'arrive from right
        xPos = Panel.Width + 10
        Randomize
        yPos = Int(Panel.Height + 21 * Rnd - 10)
End Select

Set zixl = Panel.Controls.Add("Forms.Image.1", "Zed" & zeds)

With zixl
.Width = 10
.Height = 10
.Left = xPos
.Top = yPos
.BackColor = RGB(r, g, b)
.Tag = zSp
.Enabled = False
End With

zColl.Add zeds, zixl
zStat.Add zeds, zVars

End Sub

Sub MovePlayer()

Dim tempSpeed

If upk + leftk + downk + rightk > 1 Then
    tempSpeed = speed * Sin(WorksheetFunction.Pi / 4)
Else
    tempSpeed = speed
End If

If upk = 1 And player.Top > 2 Then player.Top = player.Top - tempSpeed
If leftk = 1 And player.Left > 2 Then player.Left = player.Left - tempSpeed
If downk = 1 And player.Top < Panel.Height - 12 Then player.Top = player.Top + tempSpeed
If rightk = 1 And player.Left < Panel.Width - 12 Then player.Left = player.Left + tempSpeed

End Sub

Sub MoveZed(n)

Dim cDir()

cDir = CalcDir(zColl(n).Left, zColl(n).Top, player.Left, player.Top)

zStat(n)("bcount") = zStat(n)("bcount") + 0.1

If zColl(n).Top >= player.Top - 10 And zColl(n).Top + 10 <= player.Top + 20 And zColl(n).Left >= player.Left - 10 And zColl(n).Left + 10 <= player.Left + 20 Then
    Call ZBit(n)
    Exit Sub
Else
    zColl(n).Left = zColl(n).Left + (cDir(0) / 4 * zColl(n).Tag)
    zColl(n).Top = zColl(n).Top + (cDir(1) / 4 * zColl(n).Tag)
End If

End Sub

Sub ZBit(n)

If zStat(n)("bcount") >= zStat(n)("bmax") Then
    zStat(n)("bcount") = 0
    pstats("hp") = pstats("hp") - zStat(n)("dam")
End If

End Sub

Sub RemoveZed(n)

Frame.PtsDisp.Caption = Pts
Panel.Controls.Remove zColl.Item(n).Name
zColl.Remove n
zStat.Remove n

End Sub

Sub ShootGun()

Dim cDir()

Randomize
spreadx = firx + Int((2 * Pews(pstats("pew"))("spread") + 1) * Rnd - Pews(pstats("pew"))("spread"))

Randomize
spready = firy + Int((2 * Pews(pstats("pew"))("spread") + 1) * Rnd - Pews(pstats("pew"))("spread"))

cDir = CalcDir(player.Left + 5, player.Top + 5, spreadx, spready)

shots = shots + 1

Set blt = Panel.Controls.Add("Forms.Image.1", "Boolit" & shots)

With blt
.Width = Pews(pstats("pew"))("dam") + 1
.Height = Pews(pstats("pew"))("dam") + 1
.Left = player.Left + 5
.Top = player.Top + 5
.BackColor = RGB(0, 0, 0)
.Tag = cDir(0)
.ControlTipText = cDir(1)
End With

bColl.Add shots, blt

End Sub

Sub MoveBullet(n)

bColl(n).Left = bColl(n).Left + (bColl(n).Tag * 8)
bColl(n).Top = bColl(n).Top + (bColl(n).ControlTipText * 8)
    
End Sub

Sub BulletColl(n)

If zColl.Count > 0 Then
    For Each zkey In zColl.Keys
        If zColl(zkey).Top < bColl(n).Top + bColl(n).Height And zColl(zkey).Top + 10 > bColl(n).Top And zColl(zkey).Left < bColl(n).Left + bColl(n).Width And zColl(zkey).Left + 10 > bColl(n).Left Then
            zStat(zkey)("hp") = zStat(zkey)("hp") - Pews(pstats("pew"))("dam")
            Call RemoveBult(n)
            If zStat(zkey)("hp") <= 0 Then
                Call RemoveZed(zkey)
            End If
            Exit Sub
        End If
    Next
End If

If bColl(n).Top > Panel.Height Or bColl(n).Top < 0 Or bColl(n).Left > Panel.Width Or bColl(n).Left < 0 Then
    Call RemoveBult(n)
    Exit Sub
End If

End Sub

Sub RemoveBult(b)
    Panel.Controls.Remove bColl(b).Name
    bColl.Remove b
End Sub

Function CalcDir(x1, y1, x2, y2)

Dim dist, dX, dY, retCoor(1), t, u As Double
Dim dirX, dirY As Integer

dX = x2 - x1
dY = y2 - y1

dirX = Sgn(dX)
dirY = Sgn(dY)

dX = Abs(dX)
dY = Abs(dY)

dist = Sqr(dX ^ 2 + dY ^ 2)

If dX = 0 Then
    t = 0
ElseIf dY = 0 Then
    t = WorksheetFunction.Pi / 2
Else
    t = ArcCos((dist ^ 2 + dX ^ 2 - dY ^ 2) / (2 * dist * dX))
End If

u = WorksheetFunction.Pi / 2 - t

retCoor(0) = Sin(u) * dirX
retCoor(1) = Sin(t) * dirY

CalcDir = retCoor

End Function

Sub InitPews()

Dim pewStats1 As New Scripting.Dictionary
Dim pewStats2 As New Scripting.Dictionary
Dim pewStats3 As New Scripting.Dictionary
Dim pewStats4 As New Scripting.Dictionary

With pewStats1
.Add "dam", 1
.Add "spread", 10
.Add "proj", 1
.Add "auto", False
.Add "rof", 2
.Add "mag", 8
End With

Pews.Add "pistol", pewStats1

With pewStats2
.Add "dam", 1
.Add "spread", 60
.Add "proj", 8
.Add "auto", False
.Add "rof", 4
.Add "mag", 6
End With

Pews.Add "shotgun", pewStats2

With pewStats3
.Add "dam", 1
.Add "spread", 30
.Add "proj", 1
.Add "auto", True
.Add "rof", 0.5
.Add "mag", 30
End With

Pews.Add "smg", pewStats3

With pewStats4
.Add "dam", 3
.Add "spread", 5
.Add "proj", 1
.Add "auto", True
.Add "rof", 8
.Add "mag", 5
End With

Pews.Add "rifle", pewStats4


End Sub

Sub Wait()

tStart = Timer
Do While Timer < tStart + fps
    DoEvents
Loop

End Sub

Function ArcCos(X)

ArcCos = Atn(-X / Sqr(-X * X + 1)) + 2 * Atn(1)

End Function
